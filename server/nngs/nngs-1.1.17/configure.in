ndnl Process this file with autoconf to produce a configure script.

dnl this is to determine if the config script is running in the proper place
dnl just give it one file relative to where it should be
AC_INIT(src/nngsmain.c)

AM_CONFIG_HEADER(config.h)
AC_PREREQ(2.52)dnl               dnl Minimum Autoconf version required.

dnl this defines VERSION and PACKAGE
AM_INIT_AUTOMAKE([nngssrv], [1.1.17])
GNU_PACKAGE="GNU $PACKAGE"
AC_DEFINE_UNQUOTED(GNU_PACKAGE, "$GNU_PACKAGE")

AM_MAINTAINER_MODE

AC_PROG_CC
AC_PROG_CXX
AC_EXEEXT

dnl add -lm to library list since we use some
dnl math functions such as pow and fabs

dnl AC_SEARCH_LIBS(pow,m)
dnl AC_SEARCH_LIBS(bind,socket)
dnl AC_SEARCH_LIBS(crypt,crypt)
AC_CHECK_LIB(m,pow)
AC_CHECK_LIB(socket,bind)
AC_CHECK_LIB(crypt,crypt)

if test "x$ac_cv_mingw32" = "xyes";then
    LIBS="$LIBS -lwsock32"
fi

AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_RANLIB

dnl required since we use SUBDIRS in Makefile.am
AC_PROG_MAKE_SET

AC_HEADER_MAJOR
AC_HEADER_TIME

AC_C_CONST

AC_CHECK_HEADERS(unistd.h crypt.h term.h fcntl.h dirent.h ctype.h \
stdio.h stdlib.h string.h \
utime.h time.h errno.h strings.h signal.h endian.h \
sys/time.h sys/stat.h sys/socket.h \
arpa/inet.h arpa/telnet.h \
netinet/in.h netdb.h \
gdbm.h ndbm.h)

AC_CHECK_SIZEOF(int,4)
AC_CHECK_SIZEOF(long,4)

AC_CHECK_TYPE(ulong,unsigned long)

dnl results of setlinebuf test used in interface/play_ascii.c
dnl alarm not available in VC
dnl snprintf vsnprintf not universally available
dnl usleep not available in Unicos and mingw32
dnl random,popen,ftruncate should be in stdlib
AC_CHECK_FUNCS(setlinebuf alarm vsnprintf snprintf gettimeofday usleep \
 random popen pclose ftruncate)

dnl  ----------- special-case use of gcc ---------

dnl Not sure if we are supposed to be accessing this variable, but...

AC_SUBST(GCC_ONLY)
AC_SUBST(NNGS_SOME_WARNINGS)
AC_SUBST(NNGS_ALL_WARNINGS)
AC_SUBST(NO_WARNINGS)

dnl Please add -Wp,-lang-c89 to SOME_WARNINGS soon

NO_WARNINGS="CFLAGS=$CFLAGS"
if test $ac_cv_prog_gcc = yes; then
  GCC_ONLY=''
  GGO_SOME_WARNINGS='-Wall -W -Wpointer-arith -Wbad-function-cast -Wcast-qual -Wcast-align -Wwrite-strings -Wstrict-prototypes -Wmissing-prototypes -Wmissing-declarations -Wp,-std=c89'
  GGO_ALL_WARNINGS='-Wall -W -Wshadow -Wpointer-arith -Wbad-function-cast -Wcast-qual -Wcast-align -Wwrite-strings -Wstrict-prototypes -Wmissing-prototypes -Wmissing-declarations -Wp,-std=c89'
  NNGS_SOME_WARNINGS="CFLAGS=$CFLAGS $GGO_SOME_WARNINGS"
  NNGS_ALL_WARNINGS="CFLAGS=$CFLAGS $GGO_ALL_WARNINGS"
  CFLAGS="$CFLAGS $GGO_SOME_WARNINGS"
else
  GCC_ONLY='#'
  NNGS_SOME_WARNINGS=''
  NNGS_ALL_WARNINGS=''
fi

dnl Now lines in Makefile.am can be prefixed @GCC_ONLY@, and
dnl we can specify either @NNGS_SOME_WARNINGS@ or @NNGS_ALL_WARNINGS@

dnl FIXME POST3.0: please add warnings for other compilers !


#AM_GNU_GETTEXT
#AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

dnl hack to find the mail program(s) - may need some extra checking for
dnl different OS's/distributions. Debian symlinks mailx & Mail to 
dnl /usr/bin/mail. Defaults to the first one found.
AC_PATH_PROGS(MAILPROGRAM, mail mailx Mail)
AC_DEFINE_UNQUOTED(MAILPROGRAM, "$MAILPROGRAM")

dnl do we want ADMINLEVELS?
dnl use this for general ifdef stuff if wanted.
AC_MSG_CHECKING([whether adminlevels are requested])
AC_ARG_ENABLE(adminlevels,
  [  --enable-adminlevels     Use admin levels [default=yes]],
  if test x$enableval = "xyes"; then
      AC_MSG_RESULT([yes])
      AC_DEFINE(ADMINLEVELS)
  else
      AC_MSG_RESULT([yes])
  fi,
  AC_MSG_RESULT([no])
)

dnl <Joy> CQ: do stuff like AC_DEFINE_UNQUOTED(MY_VARIABLE,"$something")
dnl <Joy> CQ: oh yeah i think you need to #undef it in acconfig.h or something
dnl <luca> joy cq: AC_DEFINE(CQ_VARIABLE) in configure.in and one of: #undef
dnl    CQ_VARIABLE in acconfig.h or use autoheader

AC_DEFINE_UNQUOTED(AHELP_DIR,"$prefix/share/${PACKAGE}/ahelp", [admin help file dir])
AC_DEFINE_UNQUOTED(HELP_DIR,"$prefix/share/${PACKAGE}/help", [help files directory])
AC_DEFINE_UNQUOTED(INFO_DIR,"$prefix/share/${PACKAGE}/info", [info directory])
AC_DEFINE_UNQUOTED(MESSAGE_DIR,"$prefix/share/${PACKAGE}/messages", [message directory])
AC_DEFINE_UNQUOTED(PLAYER_DIR,"$prefix/share/${PACKAGE}/players", [player dirs])
AC_DEFINE_UNQUOTED(STATS_DIR,"$prefix/share/${PACKAGE}/stats", [message dir])

AC_DEFINE_UNQUOTED(GAME_DIR,"$prefix/share/${PACKAGE}/games", [games dir])
AC_DEFINE_UNQUOTED(CGAME_DIR,"$prefix/share/${PACKAGE}/cgames", [cgames dir])
AC_DEFINE_UNQUOTED(LIST_DIR,"$prefix/share/${PACKAGE}/lists", [list definition dir])
AC_DEFINE_UNQUOTED(NEWS_DIR,"$prefix/share/${PACKAGE}/news", [news files dir])
AC_DEFINE_UNQUOTED(PROBLEM_DIR,"$prefix/share/${PACKAGE}/problems", [go progblem dir])

AC_DEFINE_UNQUOTED(LADDER9_FILE,"$prefix/share/${PACKAGE}/ladder/ladder9", [9x9 ladder file])
AC_DEFINE_UNQUOTED(LADDER19_FILE,"$prefix/share/${PACKAGE}/ladder/ladder19", [19x19 ladder file])

AC_DEFINE_UNQUOTED(EMOTES_FILE,"$prefix/share/${PACKAGE}/emotestr", [emoticons file])
AC_DEFINE_UNQUOTED(INTERGO_FILE,"$prefix/share/${PACKAGE}/intergo/intergo",
                    [intergo executable from http://www.cwi.nl/~jansteen/intergo/])

dnl
dnl Files below this line are automatically generated by the server
dnl and do not need to be created.
dnl
AC_DEFINE_UNQUOTED(RATINGS_FILE,"$prefix/share/${PACKAGE}/results-rated", [file])
AC_DEFINE_UNQUOTED(NRATINGS_FILE,"$prefix/share/${PACKAGE}/ratdb", [file])
AC_DEFINE_UNQUOTED(RESULTS_FILE,"$prefix/share/${PACKAGE}/results", [file])
AC_DEFINE_UNQUOTED(NRESULTS_FILE,"$prefix/share/${PACKAGE}/nresults", [file])
AC_DEFINE_UNQUOTED(FIND_FILE,"$prefix/share/${PACKAGE}/find.out", [find file])
AC_DEFINE_UNQUOTED(NOTE_FILE,"$prefix/share/${PACKAGE}/note", [notes file])

AC_DEFINE_UNQUOTED(LOG_FILE,"logfile", [logfile- NO PATH! It resides in STATS_DIR])

dnl
dnl files found in some of the above directories
dnl
AC_DEFINE_UNQUOTED(MESS_WELCOME,"welcome", [welcome message file])
AC_DEFINE_UNQUOTED(MESS_LOGIN,"login", [login message file])
AC_DEFINE_UNQUOTED(MESS_LOGOUT,"logout", [logout message file])
AC_DEFINE_UNQUOTED(MESS_MOTD,"motd", [motd message file])
AC_DEFINE_UNQUOTED(MESS_AMOTD,"admotd", [admin motd message file])
AC_DEFINE_UNQUOTED(MESS_UNREGISTERED,"unregistered", [unreg message file])

AC_DEFINE_UNQUOTED(STATS_MESSAGES,"messages", [messages file])
AC_DEFINE_UNQUOTED(STATS_LOGONS,"logons", [login log file])
AC_DEFINE_UNQUOTED(STATS_GAMES,"games", [games file])
AC_DEFINE_UNQUOTED(STATS_RGAMES,"rgames", [rgames file])
AC_DEFINE_UNQUOTED(STATS_CGAMES,"cgames", [cgames file])

dnl
dnl define this to use PEM's malloc and smalloc routines
dnl
dnl AC_DEFINE_UNQUOTED(USE_PEM_MALLOC,, [define this to use pem's malloc and salloc routines])

dnl dmalloc debugging
AC_MSG_CHECKING([whether dmalloc debugging is requested])
AC_ARG_ENABLE(dmalloc,
  [  --enable-dmalloc         Enable dmalloc debugging [default=no]],
  if test x$enableval = "xyes"; then
      AC_MSG_RESULT([yes])
      AC_DEFINE(USING_DMALLOC)
	  DMALLOCLIB="-ldmalloc"
  else
      AC_MSG_RESULT([no])
	  DMALLOCLIB=""
  fi,
  AC_MSG_RESULT([no])
)
AC_SUBST(DMALLOCLIB)
LIBS="$LIBS $DMALLOCLIB"
AC_SUBST(LIBS)

dnl
dnl this must be the last line of configure.in
dnl
AC_OUTPUT([Makefile src/Makefile nrat/Makefile memory/Makefile data/Makefile data/help/Makefile data/ahelp/Makefile])

